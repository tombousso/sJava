(import java.util.*)

(import io.vertx.core.*)
(import io.vertx.core.buffer.Buffer)
(import io.vertx.core.http.*)
(import io.vertx.ext.web.*)
(import io.vertx.ext.web.handler.*)
(import io.vertx.ext.asyncsql.PostgreSQLClient)
(import io.vertx.ext.sql.*)
(import io.vertx.core.json.*)
(import io.netty.handler.codec.http.QueryStringEncoder)
(import org.mindrot.jbcrypt.BCrypt)
(import io.vertx.ext.web.templ.PebbleTemplateEngine)

(define-class Main (AbstractVerticle) 'public
	((main args String[]) void 'public 'static
		(define vertx (Vertx:vertx))
		(vertx:deployVerticle "Main" ((DeploymentOptions):setInstances 5))
		(println "Serving at localhost:8080")
		(println "Press enter to stop")
		(System:in:read)
		(vertx:close)
	)

	(httpClient HttpClient)
	(conn SQLConnection)
	(engine PebbleTemplateEngine)
	((<init>) void 'public
		(super:<init>)
	)
	((start fut Future{Void}) void 'public
		(define onTableCreate
			(lambda Handler{AsyncResult{Void}} (ar)
				(if (ar:succeeded)
					(this:setupRoutes fut)
					(fut:fail (ar:cause))
				)
			)
		)
		(define onConnect
			(lambda Handler{AsyncResult{SQLConnection}} (ar)
				(if (ar:succeeded)
					(begin
						(set this:conn (ar:result))
						(this:conn:execute
							"""CREATE TABLE IF NOT EXISTS users
							(
								id serial PRIMARY KEY,
								name text UNIQUE,
								pass text
							)
							WITH (
								OIDS = FALSE
							)"""
							onTableCreate
						)
					)
					(fut:fail (ar:cause))
				)
			)
		)
		(define conf (JsonObject))
		(conf:put "host" "localhost")
		(conf:put "database" "postgres")
		(conf:put "username" "postgres")
		(conf:put "password" "root")
		(define client (PostgreSQLClient:createShared this:vertx conf))
		(client:getConnection onConnect)
	)
	((stop fut Future{Void}) void 'public
		(this:conn:close (fut:completer))
	)
	((render path String rc RoutingContext) void
		(this:engine:render rc path
			(lambda Handler{AsyncResult{Buffer}} (ar)
				(if (ar:succeeded)
					((rc:response):end (ar:result))
					(rc:fail (ar:cause))
				)
			)
		)
	)
	((setupRoutes fut Future{Void}) void
		(define options (HttpClientOptions))
		(options:setSsl true)
		(set this:httpClient (this:vertx:createHttpClient options))

		(set this:engine (PebbleTemplateEngine:create this:vertx))

		(define router (Router:router this:vertx))

		((router:route):handler (BodyHandler:create))

		((router:route "/login"):handler
			(lambda Handler{RoutingContext} (rc)
				(define templ "templates/login")
				(define req (rc:request))
				(define name (req:getParam "name"))
				(define pass (req:getParam "pass"))
				(if (&& (= (req:method) HttpMethod:POST) (!= name null) (!= pass null))
					(this:conn:queryWithParams
						"SELECT (pass) FROM users WHERE name=? LIMIT 1"
						((JsonArray):add name)
						(lambda Handler{AsyncResult{ResultSet}} (ar)
							(define rs (ar:result))
							(define valid
								(&&
									(!= (rs:getNumRows) 0)
									(BCrypt:checkpw pass (((rs:getResults):get 0):getString 0))
								)
							)
							(if valid
								((rc:response):end "Success")
								(begin
									(rc:put "invalid" true)
									(this:render templ rc)
								)
							)
						)
					)
					(this:render templ rc)
				)
			)
		)

		((router:route "/register"):handler
			(lambda Handler{RoutingContext} (rc)
				(define templ "templates/register")
				(define req (rc:request))
				(define name (req:getParam "name"))
				(rc:put "name" name)
				(define pass (req:getParam "pass"))
				(define g-recaptcha (req:getParam "g-recaptcha-response"))
				(if
					(||
						(!= (req:method) HttpMethod:POST) (= name null) (= pass null) (= g-recaptcha null)
						(begin
							(define invalidName (! (name:matches "[a-zA-Z0-9]{1,100}")))
							(rc:put "invalidName" invalidName)
							invalidName
						)
						(begin
							(define invalidPass (! (pass:matches "[a-zA-Z0-9]{5,100}")))
							(rc:put "invalidPass" invalidPass)
							invalidPass
						)
					)
					(this:render templ rc)
					(begin
						(define userRes (rc:response))
						this
						(define onUpdated
							(lambda Handler{AsyncResult{UpdateResult}} (ar)
								(if (ar:succeeded)
									(userRes:end "Success")
									(begin
										(rc:put "taken" true)
										(this:render templ rc)
									)
								)
							)
						)
						(define onBody
							(lambda Handler{Buffer} (buf)
								(try
									(if ((JsonObject (buf:toString)):getBoolean "success")
										(this:conn:updateWithParams
											"INSERT INTO users (name, pass) VALUES (?, ?)"
											(with (JsonArray)
												(add name)
												(add (BCrypt:hashpw pass (BCrypt:gensalt 12)))
											)
											onUpdated
										)
										(begin
											(rc:put "recaptchaError" true)
											(this:render templ rc)
										)
									)
									(e Throwable
										(rc:fail e)
									)
								)
							)
						)
						(define gPost
							(this:httpClient:postAbs "https://www.google.com/recaptcha/api/siteverify"
								(lambda Handler{HttpClientResponse} (gRes)
									(gRes:bodyHandler onBody)
								)
							)
						)
						(define enc (QueryStringEncoder ""))
						(enc:addParam "secret" "6Lek5yYTAAAAAJeKD2b6vmU6Zu7XztzvlN3d7yq3")
						(enc:addParam "response" g-recaptcha)
						(gPost:putHeader "content-type" "application/x-www-form-urlencoded")
						(gPost:end ((enc:toString):substring 1))
					)
				)
			)
		)

		(
			((this:vertx:createHttpServer):requestHandler
				(lambda Handler{HttpServerRequest} (req)
					(router:accept req)
				)
			):listen 8080
		)

		(fut:complete)
	)
)
